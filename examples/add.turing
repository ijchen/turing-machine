# Add
# Computes the sum of two positive decimal integers. The program will write the
# sum on the tape at the home position and accept.
# 
# The expected input format is two decimal integers (as the symbols 0-9) written
# from the home position moving to the right, and separated by a single space.
# 
# The output format will be the sum of the two integers written as a decimal
# integer (as the symbols 0-9) from the home position moving to the right. The
# head will at the home position in state l
# 
# FROM                                  ⌂                                       
#...                                    95 12                                ...
#...                                    ^ state: a                           ...
# TO                                    ⌂                                       
#...                                    107                                  ...
#...                         (ACCEPTED) ^ state: l                           ...

INITIAL STATE: a
BLANK SYMBOL: [ ]

TRANSITIONS:

# Move from somewhere in the first number to the rightmost digit of the second
# number
# FROM                                  ⌂                                       
#...                                    9834 59586                           ...
#...                                    ^ state: a                           ...
# TO                           ⌂                                                
#...                           9834 59586                                    ...
#...                                    ^ state: c                           ...
a0: 0>a # Still going through first number, keep sliding right
a1: 1>a # Still going through first number, keep sliding right
a2: 2>a # Still going through first number, keep sliding right
a3: 3>a # Still going through first number, keep sliding right
a4: 4>a # Still going through first number, keep sliding right
a5: 5>a # Still going through first number, keep sliding right
a6: 6>a # Still going through first number, keep sliding right
a7: 7>a # Still going through first number, keep sliding right
a8: 8>a # Still going through first number, keep sliding right
a9: 9>a # Still going through first number, keep sliding right
a :  >b # Hit end of first number, start sliding through second number
b0: 0>b # Still going through second number, keep sliding right
b1: 1>b # Still going through second number, keep sliding right
b2: 2>b # Still going through second number, keep sliding right
b3: 3>b # Still going through second number, keep sliding right
b4: 4>b # Still going through second number, keep sliding right
b5: 5>b # Still going through second number, keep sliding right
b6: 6>b # Still going through second number, keep sliding right
b7: 7>b # Still going through second number, keep sliding right
b8: 8>b # Still going through second number, keep sliding right
b9: 9>b # Still going through second number, keep sliding right
b :  <c # Hit end of second number

# Decrement the second number
# FROM                         ⌂                                                
#...                           9840 59580                                    ...
#...                                    ^ (c)                                ...
# TO                                 ⌂                                          
#...                                 9840 59579                              ...
#...                                    ^ (e)                                ...
c1: 0<d # If we hit a 1, decrement it to 0 and go increment the first number
c2: 1<d # If we hit a 2, decrement it to 1 and go increment the first number
c3: 2<d # If we hit a 3, decrement it to 2 and go increment the first number
c4: 3<d # If we hit a 4, decrement it to 3 and go increment the first number
c5: 4<d # If we hit a 5, decrement it to 4 and go increment the first number
c6: 5<d # If we hit a 6, decrement it to 5 and go increment the first number
c7: 6<d # If we hit a 7, decrement it to 6 and go increment the first number
c8: 7<d # If we hit a 8, decrement it to 7 and go increment the first number
c9: 8<d # If we hit a 9, decrement it to 8 and go increment the first number
c0: 9<c # If we hit a 0, add 9 and decrement the next digit over (10x this one)
c :  >i # Underflow indicates decrementing 0 - we're done adding
d0: 0<d # Still going through second number, keep sliding right
d1: 1<d # Still going through second number, keep sliding right
d2: 2<d # Still going through second number, keep sliding right
d3: 3<d # Still going through second number, keep sliding right
d4: 4<d # Still going through second number, keep sliding right
d5: 5<d # Still going through second number, keep sliding right
d6: 6<d # Still going through second number, keep sliding right
d7: 7<d # Still going through second number, keep sliding right
d8: 8<d # Still going through second number, keep sliding right
d9: 9<d # Still going through second number, keep sliding right
d :  <e # Hit end of second number

# Increment first number
# FROM                               ⌂                                          
#...                                 9840 59579                              ...
#...                                    ^ state: e                           ...
# TO                           ⌂                                                
#...                           9841 59579                                    ...
#...                                    ^ state: c                           ...
e0: 1>a # If we hit a 0, increment it to 1 and go decrement the second number
e1: 2>a # If we hit a 1, increment it to 2 and go decrement the second number
e2: 3>a # If we hit a 2, increment it to 3 and go decrement the second number
e3: 4>a # If we hit a 3, increment it to 4 and go decrement the second number
e4: 5>a # If we hit a 4, increment it to 5 and go decrement the second number
e5: 6>a # If we hit a 5, increment it to 6 and go decrement the second number
e6: 7>a # If we hit a 6, increment it to 7 and go decrement the second number
e7: 8>a # If we hit a 7, increment it to 8 and go decrement the second number
e8: 9>a # If we hit a 8, increment it to 9 and go decrement the second number
e9: 0<e # If we hit a 9, sub 9 and increment the next digit over (10x this one)
e :  >f # If incrementing overflowed, shift everything right to make space

# Print a 1 in the current position, shifting everything once to the right
# FROM                                  ⌂                                       
#...                                    0000 59420                           ...
#...                                    ^ state: f                           ...
# TO                          ⌂                                                 
#...                          10000 59420                                    ...
#...                                    ^ state: c                           ...
f0: 1>g # Print a 1 and start copying zeros right through the first number
g0: 0>g # Still copying zeros right through the first number
g : 0>h # Hit end of the first number, copy the in-between blank symbol right
h0:  >0 # Print the blank in-between symbol and copy 0 right
h1:  >1 # Print the blank in-between symbol and copy 1 right
h2:  >2 # Print the blank in-between symbol and copy 2 right
h3:  >3 # Print the blank in-between symbol and copy 3 right
h4:  >4 # Print the blank in-between symbol and copy 4 right
h5:  >5 # Print the blank in-between symbol and copy 5 right
h6:  >6 # Print the blank in-between symbol and copy 6 right
h7:  >7 # Print the blank in-between symbol and copy 7 right
h8:  >8 # Print the blank in-between symbol and copy 8 right
h9:  >9 # Print the blank in-between symbol and copy 9 right
00: 0>0 # Print the previous 0 and copy 0 right
01: 0>1 # Print the previous 0 and copy 1 right
02: 0>2 # Print the previous 0 and copy 2 right
03: 0>3 # Print the previous 0 and copy 3 right
04: 0>4 # Print the previous 0 and copy 4 right
05: 0>5 # Print the previous 0 and copy 5 right
06: 0>6 # Print the previous 0 and copy 6 right
07: 0>7 # Print the previous 0 and copy 7 right
08: 0>8 # Print the previous 0 and copy 8 right
09: 0>9 # Print the previous 0 and copy 9 right
10: 1>0 # Print the previous 1 and copy 0 right
11: 1>1 # Print the previous 1 and copy 1 right
12: 1>2 # Print the previous 1 and copy 2 right
13: 1>3 # Print the previous 1 and copy 3 right
14: 1>4 # Print the previous 1 and copy 4 right
15: 1>5 # Print the previous 1 and copy 5 right
16: 1>6 # Print the previous 1 and copy 6 right
17: 1>7 # Print the previous 1 and copy 7 right
18: 1>8 # Print the previous 1 and copy 8 right
19: 1>9 # Print the previous 1 and copy 9 right
20: 2>0 # Print the previous 2 and copy 0 right
21: 2>1 # Print the previous 2 and copy 1 right
22: 2>2 # Print the previous 2 and copy 2 right
23: 2>3 # Print the previous 2 and copy 3 right
24: 2>4 # Print the previous 2 and copy 4 right
25: 2>5 # Print the previous 2 and copy 5 right
26: 2>6 # Print the previous 2 and copy 6 right
27: 2>7 # Print the previous 2 and copy 7 right
28: 2>8 # Print the previous 2 and copy 8 right
29: 2>9 # Print the previous 2 and copy 9 right
30: 3>0 # Print the previous 3 and copy 0 right
31: 3>1 # Print the previous 3 and copy 1 right
32: 3>2 # Print the previous 3 and copy 2 right
33: 3>3 # Print the previous 3 and copy 3 right
34: 3>4 # Print the previous 3 and copy 4 right
35: 3>5 # Print the previous 3 and copy 5 right
36: 3>6 # Print the previous 3 and copy 6 right
37: 3>7 # Print the previous 3 and copy 7 right
38: 3>8 # Print the previous 3 and copy 8 right
39: 3>9 # Print the previous 3 and copy 9 right
40: 4>0 # Print the previous 4 and copy 0 right
41: 4>1 # Print the previous 4 and copy 1 right
42: 4>2 # Print the previous 4 and copy 2 right
43: 4>3 # Print the previous 4 and copy 3 right
44: 4>4 # Print the previous 4 and copy 4 right
45: 4>5 # Print the previous 4 and copy 5 right
46: 4>6 # Print the previous 4 and copy 6 right
47: 4>7 # Print the previous 4 and copy 7 right
48: 4>8 # Print the previous 4 and copy 8 right
49: 4>9 # Print the previous 4 and copy 9 right
50: 5>0 # Print the previous 5 and copy 0 right
51: 5>1 # Print the previous 5 and copy 1 right
52: 5>2 # Print the previous 5 and copy 2 right
53: 5>3 # Print the previous 5 and copy 3 right
54: 5>4 # Print the previous 5 and copy 4 right
55: 5>5 # Print the previous 5 and copy 5 right
56: 5>6 # Print the previous 5 and copy 6 right
57: 5>7 # Print the previous 5 and copy 7 right
58: 5>8 # Print the previous 5 and copy 8 right
59: 5>9 # Print the previous 5 and copy 9 right
60: 6>0 # Print the previous 6 and copy 0 right
61: 6>1 # Print the previous 6 and copy 1 right
62: 6>2 # Print the previous 6 and copy 2 right
63: 6>3 # Print the previous 6 and copy 3 right
64: 6>4 # Print the previous 6 and copy 4 right
65: 6>5 # Print the previous 6 and copy 5 right
66: 6>6 # Print the previous 6 and copy 6 right
67: 6>7 # Print the previous 6 and copy 7 right
68: 6>8 # Print the previous 6 and copy 8 right
69: 6>9 # Print the previous 6 and copy 9 right
70: 7>0 # Print the previous 7 and copy 0 right
71: 7>1 # Print the previous 7 and copy 1 right
72: 7>2 # Print the previous 7 and copy 2 right
73: 7>3 # Print the previous 7 and copy 3 right
74: 7>4 # Print the previous 7 and copy 4 right
75: 7>5 # Print the previous 7 and copy 5 right
76: 7>6 # Print the previous 7 and copy 6 right
77: 7>7 # Print the previous 7 and copy 7 right
78: 7>8 # Print the previous 7 and copy 8 right
79: 7>9 # Print the previous 7 and copy 9 right
80: 8>0 # Print the previous 8 and copy 0 right
81: 8>1 # Print the previous 8 and copy 1 right
82: 8>2 # Print the previous 8 and copy 2 right
83: 8>3 # Print the previous 8 and copy 3 right
84: 8>4 # Print the previous 8 and copy 4 right
85: 8>5 # Print the previous 8 and copy 5 right
86: 8>6 # Print the previous 8 and copy 6 right
87: 8>7 # Print the previous 8 and copy 7 right
88: 8>8 # Print the previous 8 and copy 8 right
89: 8>9 # Print the previous 8 and copy 9 right
90: 9>0 # Print the previous 9 and copy 0 right
91: 9>1 # Print the previous 9 and copy 1 right
92: 9>2 # Print the previous 9 and copy 2 right
93: 9>3 # Print the previous 9 and copy 3 right
94: 9>4 # Print the previous 9 and copy 4 right
95: 9>5 # Print the previous 9 and copy 5 right
96: 9>6 # Print the previous 9 and copy 6 right
97: 9>7 # Print the previous 9 and copy 7 right
98: 9>8 # Print the previous 9 and copy 8 right
99: 9>9 # Print the previous 9 and copy 9 right
0 : 0=c # Done shifting - print the previous 0 and continue adding
1 : 1=c # Done shifting - print the previous 1 and continue adding
2 : 2=c # Done shifting - print the previous 2 and continue adding
3 : 3=c # Done shifting - print the previous 3 and continue adding
4 : 4=c # Done shifting - print the previous 4 and continue adding
5 : 5=c # Done shifting - print the previous 5 and continue adding
6 : 6=c # Done shifting - print the previous 6 and continue adding
7 : 7=c # Done shifting - print the previous 7 and continue adding
8 : 8=c # Done shifting - print the previous 8 and continue adding
9 : 9=c # Done shifting - print the previous 9 and continue adding

# Erase underflow 9s from the second number and return to the home position
# FROM                            ⌂                                             
#...                              69420 99999                                ...
#...                                    ^ state: i                           ...
# TO                                    ⌂                                       
#...                                    69420                                ...
#...                                    ^ state: l                           ...
i9:  >i # Erase the 9 and keep erasing 9s
i :  <j # Hit the end of the underflow 9s, go left until we hit the sum
j :  <j # Keep sliding left until we hit the sum
j0: 0<k # Hit the sum - go left until we hit the start of the sum
j1: 1<k # Hit the sum - go left until we hit the start of the sum
j2: 2<k # Hit the sum - go left until we hit the start of the sum
j3: 3<k # Hit the sum - go left until we hit the start of the sum
j4: 4<k # Hit the sum - go left until we hit the start of the sum
j5: 5<k # Hit the sum - go left until we hit the start of the sum
j6: 6<k # Hit the sum - go left until we hit the start of the sum
j7: 7<k # Hit the sum - go left until we hit the start of the sum
j8: 8<k # Hit the sum - go left until we hit the start of the sum
j9: 9<k # Hit the sum - go left until we hit the start of the sum
k0: 0<k # Keep sliding left until we hit the start of the sum
k1: 1<k # Keep sliding left until we hit the start of the sum
k2: 2<k # Keep sliding left until we hit the start of the sum
k3: 3<k # Keep sliding left until we hit the start of the sum
k4: 4<k # Keep sliding left until we hit the start of the sum
k5: 5<k # Keep sliding left until we hit the start of the sum
k6: 6<k # Keep sliding left until we hit the start of the sum
k7: 7<k # Keep sliding left until we hit the start of the sum
k8: 8<k # Keep sliding left until we hit the start of the sum
k9: 9<k # Keep sliding left until we hit the start of the sum
k :  >l # Hit the end of the sum - we are done!

# Accept
# FROM                                  ⌂                                       
#...                                    69420                                ...
#...                                    ^ state: l                           ...
# TO                                    ⌂                                       
#...                                    69420                                ...
#...                         (ACCEPTED) ^ state: l                           ...
l0: ACC # Accept
l1: ACC # Accept
l2: ACC # Accept
l3: ACC # Accept
l4: ACC # Accept
l5: ACC # Accept
l6: ACC # Accept
l7: ACC # Accept
l8: ACC # Accept
l9: ACC # Accept